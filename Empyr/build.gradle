apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources","DuplicatePlatformClasses"
            textOutput "stdout"
            textReport true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set("javadoc")
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

signing {
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.empyr'
                artifactId 'android'

                artifact androidJavadocsJar
                artifact androidSourcesJar

                pom {
                    name = 'Empyr Android Library'
                    description = 'This library is intended to help ease the integration effort for partners that participate in the Empyr Offer Platform.'
                    url = 'https://github.com/EmpyrNetwork/empyr_android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    organization {
                        name = 'Empyr'
                        url = 'https://www.empyr.com'
                    }

                    developers {
                        developer {
                            name = 'Empyr Development'
                            email = 'developer@empyr.com'
                        }
                    }

                    scm {
                        connection = "sscm:git:https://github.com/EmpyrNetwork/empyr_android.git"
                        developerConnection = "scm:git:git@github.com:EmpyrNetwork/empyr_android.git"
                        url = "https://github.com/EmpyrNetwork/empyr_android"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.empyr:client:0.9.17'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


